window.Order =
  updateTotalPrice: ->
    totalPrice = 0.0
    credit = parseFloat($('#user').data('credit')) || 0
    
    $('.order_line:not([data-exclude-from-total])').each ->
      totalPrice += parseFloat($(this).data('price')) || 0
    
    if totalPrice > 0 && (credit >= (totalPrice * Order.threshold))
      $('#not_printed').hide()
      $('#printed').show()
    else if totalPrice > 0
      $('#printed').hide()
      $('#not_printed').show()
    
    money = $('#total span.money')
    
    money.html(money.html().replace(/(\d+.)+\d+/, totalPrice.toFixed(3)))
  
  updateOrderLinePrice: (orderLine)->
    Jobs.updatePricePerCopy('.order_line')
    
    copies = parseInt orderLine.find('input[name$="[copies]"]').val()
    pages = parseInt orderLine.find('input[name$="[pages]"]').val()
    pricePerCopy = parseFloat(
      orderLine.find('input[name$="[price_per_copy]"]').val()
    )
    pricePerOneSidedCopy = PriceChooser.choose(
      $('#total_pages').data('pricePerOneSided'), parseInt($('#total_pages').val())
    )
    evenRange = pages - (pages % 2)
    rest = (pages % 2) * pricePerOneSidedCopy
    olPrice = (copies * (pricePerCopy * evenRange + rest)) || 0
    money = orderLine.find('span.money')

    orderLine.data 'price', olPrice.toFixed(3)
    money.html(money.html().replace(/(\d+.)+\d+/, olPrice.toFixed(3)))

    Order.updateTotalPrice()

  updateOrderTotalPages: ->
    totalPages = 0

    $('.order_line').each ->
      copies = parseInt $(this).find('input[name$="[copies]"]').val()
      pages = parseInt $(this).find('input[name$="[pages]"]').val()

      totalPages += (copies || 0) * (pages || 0)

    $('#total_pages').val totalPages
    $('.order_line').each -> Order.updateOrderLinePrice($(this))

jQuery ($)->
  if $('#ph_orders').length > 0
    $(document).on 'ajax:success', 'a.details-link', (event, data)->
      console.log(event)
      console.log(data)
      Helper.show(
        $(this).parents('.order_line').find('.dynamic_details').hide().html(data)
      )
    
    $(document).on 'item.removed', (event, element)->
      if $(element).hasClass('order_line')
        $(element).attr('data-exclude-from-total', '1')
        .find('.page-modifier:first').trigger('ph.page_modification')

        Order.updateOrderTotalPages()
    
    Jobs.listenTwoSidedChanges('.order_line')

    $(document).on 'change keyup', '.price-modifier', ->
      Order.updateOrderLinePrice $(this).parents('.order_line')

    $(document).on 'change keyup ph.page_modification', '.page-modifier', ->
      Order.updateOrderTotalPages()
      
    $(document).on 'click', 'a[data-action="print"]', (event)->
      window.print()
      
      event.preventDefault()
      event.stopPropagation()

    # Subir un archivo para agregarlo a la orden
    $('input:file').fileupload
      dataType: 'script'
      add: (e, data) ->
        type = /(pdf)$/i
        file = data.files[0]

        if type.test(file.type) || type.test(file.name)
          $('#file-upload-error').hide()
          $('.progress.hide').toggle('slow')
          $('#upload-file .file').toggle('slow')
          $('input:submit').attr('disabled', true)
          data.submit()
        else
          $('#file-upload-error').show('slow')
      
      progressall: (e, data) ->
        progress = parseInt(data.loaded / data.total * 100, 10)
        $('.progress .bar').css('width', progress + '%')

      done: (e, data) ->
        $('.progress.hide').toggle('slow')
        $('#upload-file .file').toggle('slow')
        $('input:submit').attr('disabled', false)
        Order.updateOrderTotalPages()
        State.fileUploaded = true

    # No dejar la pagina sino las subidas se pierden
    $(window).bind 'beforeunload', ->
      Messages.fileUploadWarning if State.fileUploaded &&
        !$('input:submit').attr('disabled')
    
    $('#clear_catalog_order_link').on 'click', () ->
      State.fileUploaded = false
